/// SPDX-LICENSE-IDENTIFIER: MIT
pragma solidity ^0.8.4;

import "portfolio/strategies/NormalStrategyLib.sol";
import { console2 as logger } from "forge-std/console2.sol";

function toInt(uint256 x) pure returns (int256) {
    if (x > uint256(type(int256).max)) revert("toInt: overflow");
    return int256(x);
}

function toUint(int256 x) pure returns (uint256) {
    if (x < 0) return 0;

    return uint256(x);
}

library ExtendedNormalCurveLib {
    using Gaussian for *;
    using FixedPointMathLib for *;
    using { toInt } for uint256;
    using { toUint } for int256;

    error ExtendedNormalCurveLib_InvalidGammaPct(uint256);

    /// @dev Î”1 = ð›¾âˆ’1(1 âˆ’ ð‘…1 âˆ’ Î¦(Î¦âˆ’1 (1 âˆ’ ð‘…1) + ln(1 + ðœ–)/ðœŽâˆšðœ)).
    function computeXInToMatchReportedPrice(
        NormalCurve memory self,
        uint currentPriceWad,
        uint256 desiredPriceWad,
        uint256 gammaPctWad
    ) internal pure returns (uint256 deltaX) {
        uint epsilonScalar = desiredPriceWad.mulWadDown(gammaPctWad).divWadDown(currentPriceWad);
        // 1 - R1
        int256 oneMinusR1 = WAD.toInt() - self.reserveXPerWad.toInt();

        // Î¦âˆ’1 (1 âˆ’ ð‘…1)
        int256 cdfInvOneMinusR1 = oneMinusR1.ppf();

        // ln(1 + ðœ–)
        int256 logOnePlusEpsilon =
            epsilonScalar.toInt().lnWad();

        // ln(1 + ðœ–)/ðœŽâˆšðœ
        int256 logOnePlusEpsilonStdDevSqrtTau = (
            logOnePlusEpsilon * WAD.toInt() / self.computeStdDevSqrtTau().toInt()
        );

        // Î¦âˆ’1 (1 âˆ’ ð‘…1) + ln(1 + ðœ–)/ðœŽâˆšðœ
        int256 cdfInvOneMinusR1PlusLogOnePlusEpsilonStdDevSqrtTau =
            (cdfInvOneMinusR1 + logOnePlusEpsilonStdDevSqrtTau);

    
        int256 result = oneMinusR1
            - cdfInvOneMinusR1PlusLogOnePlusEpsilonStdDevSqrtTau.cdf();
        return uint256(result).mulWadDown(gammaPctWad);
    }

    /// @dev âˆ†Î± = (1 âˆ’ RÎ± âˆ’ Î¦( ln(m/Î³K) ÏƒâˆšÏ„ + 1/2ÏƒâˆšÏ„)) / Î³
    function computeXInputGivenMarginalPrice(
        NormalCurve memory self,
        uint256 marginalPriceWad,
        uint256 gammaPctWad
    ) internal pure returns (uint256 deltaX) {
        if (gammaPctWad == 0) {
            revert ExtendedNormalCurveLib_InvalidGammaPct(gammaPctWad);
        }
        // ÏƒâˆšÏ„
        uint256 stdDevSqrtTau = self.computeStdDevSqrtTau();

        // ln(m/Î³K)
        int256 logResult = marginalPriceWad.divWadDown(
            gammaPctWad.mulWadDown(self.strikePriceWad)
        ).toInt().lnWad();

        // ln(m/Î³K) ÏƒâˆšÏ„ + 1/2ÏƒâˆšÏ„
        int256 cdfInput = (
            logResult * stdDevSqrtTau.toInt() / WAD.toInt()
                + stdDevSqrtTau.toInt() / 2
        );

        // 1 - RÎ± âˆ’ Î¦( ln(m/Î³K) ÏƒâˆšÏ„ + 1/2ÏƒâˆšÏ„)
        logger.logInt(logResult);
        logger.logInt(cdfInput);
        logger.logInt(cdfInput.cdf());
        int256 result =
            WAD.toInt() - self.reserveXPerWad.toInt() - cdfInput.cdf();

        logger.logInt(result);

        return result.toUint().divWadDown(gammaPctWad);
    }

    /// @dev âˆ†Î² = (KÎ¦( ln(m/K) ÏƒâˆšÏ„ âˆ’ 1/2 ÏƒâˆšÏ„) + k âˆ’ RÎ²) / Î³
    function computeYInputGivenMarginalPrice(
        NormalCurve memory self,
        uint256 marginalPriceWad,
        uint256 gammaPctWad,
        int256 invariant // k
    ) internal pure returns (uint256 deltaY) {
        if (gammaPctWad == 0) {
            revert ExtendedNormalCurveLib_InvalidGammaPct(gammaPctWad);
        }

        // ÏƒâˆšÏ„
        uint256 stdDevSqrtTau = self.computeStdDevSqrtTau();

        // ln(m/K)
        int256 logResult =
            marginalPriceWad.divWadDown(self.strikePriceWad).toInt().lnWad();

        // ln(m/K) ÏƒâˆšÏ„ âˆ’ 1/2 ÏƒâˆšÏ„
        int256 cdfInput = (
            logResult * stdDevSqrtTau.toInt() / WAD.toInt()
                - stdDevSqrtTau.toInt() / 2
        );

        // KÎ¦( ln(m/K) ÏƒâˆšÏ„ âˆ’ 1/2 ÏƒâˆšÏ„)
        int256 result = (
            self.strikePriceWad.toInt() * cdfInput.cdf() / WAD.toInt()
                + invariant - self.reserveYPerWad.toInt()
        );

        return result.toUint().divWadDown(gammaPctWad);
    }
}
